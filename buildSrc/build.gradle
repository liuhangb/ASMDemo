plugins {
    id 'java-library'
    id 'groovy'
    id 'maven'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.android.tools.build:gradle:3.5.0'
}

repositories {
    maven { url "https://maven.meitu.city/" }
    google()
    jcenter()
}


def groupId = 'com.meitu.library.transform'
def artifactId = 'custom-transform'
def version = '1.0.1'

def localReleaseDest = uri(rootProject.file('repo'))

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = groupId
            pom.artifactId = artifactId
            pom.version = version
            // Add other pom properties here if you want (developer details / licenses)
            repository(url: "file://${localReleaseDest}")
        }
    }
}


task zipRelease(type: Zip) {
    from localReleaseDest
    destinationDir buildDir
    archiveName "release-${version}.zip"
}

task generateRelease {
    doLast {
        println "Release ${version} can be found at ${localReleaseDest}/"
        println "Release ${version} zipped can be found ${buildDir}/release-${version}.zip"
    }
}

generateRelease.dependsOn(uploadArchives)
generateRelease.dependsOn(zipRelease)